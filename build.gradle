plugins {
    id 'java'
    id 'scala'
    id 'application'
    id 'maven-publish'
}

group = 'io.github.noeppi_noeppi.tools'

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = 'https://maven.parchmentmc.org/' }
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.13.6'
    implementation 'org.scala-lang.modules:scala-parser-combinators_2.13.0-RC3:1.1.2'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'net.minecraftforge:srgutils:0.4.3'
    implementation 'org.ow2.asm:asm:9.2'
    implementation 'org.ow2.asm:asm-tree:9.2'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.eclipse.jdt:org.eclipse.jdt.core:3.26.0'
    implementation 'org.parchmentmc:feather:0.6.5'
    implementation 'org.parchmentmc.feather:io-gson:0.6.5'
}

application.mainClassName = 'io.github.noeppi_noeppi.tools.sourcetransform.Main'

jar {
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}

task fatjar(type: Jar) {
    archiveClassifier = "fatjar"
    manifest = jar.manifest
    with jar
}
build.dependsOn fatjar

task configureFatJar {
    doLast {
        configurations.runtimeClasspath.each { dep ->
            fatjar.from(project.zipTree(dep)) {
                exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
    outputs.upToDateWhen { false }
}
fatjar.dependsOn configureFatJar

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}
build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            artifact jar
            artifact fatjar
            artifact sourcesJar
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url '../MinecraftUtilities/maven'
        }
    }
}