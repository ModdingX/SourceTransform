plugins {
    id 'java'
    id 'scala'
    id 'application'
    id 'maven-publish'
}

group = 'org.moddingx'
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = 'https://maven.parchmentmc.org/' }
}

dependencies {
    implementation 'org.scala-lang:scala3-library_3:3.2.0'
    implementation 'org.scala-lang.modules:scala-parser-combinators_3:2.1.1'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'net.minecraftforge:srgutils:0.4.13'
    implementation 'org.ow2.asm:asm:9.4'
    implementation 'org.ow2.asm:asm-tree:9.4'
    implementation 'org.ow2.asm:asm-util:9.4'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.eclipse.jdt:org.eclipse.jdt.core:3.32.0'
    implementation 'org.parchmentmc:feather:1.0.0.3'
    implementation 'org.parchmentmc.feather:io-gson:1.0.0.3'
}

application.mainClassName = 'org.moddingx.sourcetransform.Main'

jar {
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}

task fatjar(type: Jar) {
    archiveClassifier = "fatjar"
    manifest = jar.manifest
    with jar
}
build.dependsOn fatjar

task configureFatJar {
    doLast {
        configurations.runtimeClasspath.each { dep ->
            fatjar.from(project.zipTree(dep)) {
                exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
    outputs.upToDateWhen { false }
}
fatjar.dependsOn configureFatJar

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}
build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url '../moddingx/maven'
        }
    }
}
